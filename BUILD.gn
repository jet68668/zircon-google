# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("$zx/public/gn/config/standard.gni")
import("$zx/public/gn/host_tool_action.gni")
import("$zx/public/gn/toolchain/select_toolchain.gni")
import("$zx/public/gn/zbi.gni")

# Build the native host tools intended for developer use.
foreach(host, standard_build_hosts) {
  select_toolchain("tools-${host.os}-${host.cpu}") {
    testonly = true
    environment_label = "$zx/public/gn/toolchain:host"
    cpu = host.cpu
    os = host.os
    deps = [
      "$zx/system/host:tools",
    ]
  }
}

# Install the host developer tools for the running host platform in the
# "$root_build_dir/tools/" directory.
tools_rspfile = "$target_gen_dir/tools.rsp"
write_data("tools._rsp") {
  visibility = [ ":tools" ]
  testonly = true
  outputs = [
    tools_rspfile,
  ]
  deps = [
    ":tools-${host_os}-${host_cpu}",
  ]
  output_conversion = "list lines"
  data_keys = [ "tool_executables" ]
}

action("tools") {
  testonly = true
  deps = [
    ":tools._rsp",
  ]
  script = "$zx/scripts/copy-files"
  depfile = "$target_out_dir/$target_name.d"
  outputs = [
    depfile,
  ]
  sources = [
    tools_rspfile,
  ]
  args = [
    "tools",
    rebase_path(depfile, root_build_dir),
    rebase_path(tools_rspfile, root_build_dir),
  ]
}

public_targets = [
  "bootloader",
  "kernel",
  "core",
]

testonly_public_targets = [
  "bin",
  "drivers",
]

# Each cpu is a target name for all the public targets for that CPU.
foreach(cpu, standard_fuchsia_cpus) {
  group(cpu) {
    testonly = true
    deps = []
    foreach(target, public_targets + testonly_public_targets) {
      deps += [ "$zx/public/$target:$cpu" ]
    }
  }
}

# Each public target is a target name for all the CPUs for that target.
foreach(target, public_targets + testonly_public_targets) {
  group(target) {
    testonly = public_targets + [ target ] - [ target ] == public_targets
    deps = []
    foreach(cpu, standard_fuchsia_cpus) {
      deps += [ "$zx/public/$target:$cpu" ]
    }
  }
}

group("all-cpu") {
  testonly = true
  deps = []
  foreach(cpu, standard_fuchsia_cpus) {
    deps += [ ":$cpu" ]
  }
}

group("default") {
  testonly = true
  deps = [
    ":all-cpu",
    ":tools",
  ]
}

# TODO: example

bootimage("sherlock-zedboot") {
  cpu = "arm64"
  deps = [
    ":zedboot",
    "$zx/system/dev/board/sherlock",
  ]
}

if (current_cpu != "") {
  group("zedboot") {
    deps = [
      "$zx/public/core",
      # TODO: some group() of useful diag/debug tools
    ]
    metadata = {
      devmgr_config = [ "netsvc.netboot=true" ]
    }
  }

  group("recovery") {
    deps = [
      "$zx/public/core",
      # TODO: some group() of useful diag/recovery tools
    ]
  }
}
