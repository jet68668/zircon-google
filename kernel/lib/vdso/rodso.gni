# Copyright 2018 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("$zx/public/gn/toolchain/c_utils.gni")

template("rodso_code_header") {
  toolchain_utils_action(target_name) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "testonly",
                             "visibility",
                           ])

    outputs = invoker.outputs
    assert(outputs == [ outputs[0] ],
           "rodso_code_header() outputs must have exactly one element")

    script = "$zx/scripts/gen-rodso-code.sh"
    utils = [
      "nm",
      "readelf",
    ]
    sources = get_metadata([ "" ], [ "link_output" ], [ "link_barrier" ], true)
    args = rebase_path(outputs, root_build_dir) + [ invoker.name ] +
           get_metadata([ "" ], [ "link_output_arg" ], [ "link_barrier" ])
  }
}
